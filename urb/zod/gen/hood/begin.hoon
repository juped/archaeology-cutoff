::
::::  /hoon/begin/hood/gun
  ::
/?  314
/-  sole
::
::::
  !:
=>  |%
    ++  begs  ,[his=@p tic=@p yen=@t ges=gens]
    ++  scug  |*([a=@ b=(pole)] ?~(b ~ ?~(a ~ [-.b $(b +.b, a (dec a))])))
    --
[sole .]
:-  %dog
|=  $:  [now=@da eny=@uvI bec=beak]
        [arg=_(scug *@ ~[his=*@p tic=*@p]) ~]
    ==
^-  (sole-result (cask begs))
=-  ?~  arg  -
    =+  (fun.q.q his.arg)
    ?~  +.arg  -
    (fun.q.q tic.arg)
%+  sole-lo
  [%& %helm-begin "your ship: ~"]
%+  sole-go  fed:ag
|=  his=@p
%+  sole-lo
  [%& %helm-ticket "your ticket: ~"]
%+  sole-go  fed:ag
|=  tic=@p
%+  sole-lo  
  [%& %helm-entropy "some entropy: "]
%+  sole-go  (boss 256 (more gon qit))
|=  yen=@t
=+  ney=(shax yen)
%+  sole-yo  `tank`[%leaf "entropy check: {(scow %p `@p`(mug ney))}"]
%+  sole-so  %helm-begin
:*  his
    tic
    ney
::
    ^-  gens
    :-  %en
    =+  can=(clan his)
    ?-  can
      %czar  [%czar ~]
      %duke  [%duke %anon ~]
      %earl  [%earl (scot %p his)]
      %king  [%king ?:(=(~doznec his) 'Urban Republic' (scot %p his))]
      %pawn  [%pawn ~]
    ==
==
