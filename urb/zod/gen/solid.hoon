::
::::  /hoon/solid/gen
  ::
/?  314
::
::::
  !:
:-  %say
|=  $:  {now+@da eny+@uvI bec+beak}
        {$~ $~}
    ==
:-  %noun
=+  top=`path`/(scot %p p.bec)/[q.bec]/(scot %da now)/arvo
=+  pax=`path`(weld top `path`[%hoon ~])
~&  %solid-start
=+  gen=(reck pax)
~&  %solid-parsed
=+  ken=q:(~(mint ut %noun) %noun gen)
~&  %solid-compiled
:-  ken
=+  all=.*(0 ken)
=+  ^=  vay  ^-  (list {p+@tas q+@tas})
    :~  [%$ %zuse]
        [%g %gall]
        [%f %ford]
        [%a %ames]
        [%b %behn]
        [%c %clay]
        [%d %dill]
        [%e %eyre]
    ==
|-  ^+  all
?~  vay  all
=+  pax=(weld top `path`[q.i.vay ~])
=+  txt=((hard @) .^(%cx (weld pax `path`[%hoon ~])))
=+  sam=[now `ovum`[[%gold ~] [%veer p.i.vay pax txt]]]
~&  [%solid-veer i.vay]
=+  gat=.*(all .*(all [0 42]))
=+  nex=+:.*([-.gat [sam +>.gat]] -.gat)
$(vay t.vay, all nex)
