|%
++  sign
  $%  $:  %g
  $%  [%nice ~]
      [%rush %txt p=cord]
      [%rush %tang p=(list tank)]
  ==  ==  ==
--
::
!:
|_  [hid=hide arg=cord]
++  poke-grep-args
  |=  [ost=bone you=ship arg=cord ~]
  =.  ^arg  arg
  :_  +>.$
  :~  [ost %pass / %g %show [our.hid +.imp.hid] you /in/[-.imp.hid]]
  ==
++  peer
  |=
    *
  `+>
++  pour
  |=  [ost=bone pax=path sih=*]
  =+  sih=;;(sign sih)
  ?^  pax  `+>.$
  :_  +>.$
  ?-    +<.sih
      %nice
    [ost %give %nice ~]~
      %rush
    =+  lin=?-(+>-.sih %txt (trip p.sih), %tang ~(ram re -.p.sih))
    =+  tag=(trip arg)
    %-  spam
    |-
    ?:  =(tag (scag (lent tag) lin))
      +.sih
    ?~  lin
      [%rush %tang ~]
    $(lin t.lin)
  ==
++  spam
  |*  git=*
  %+  turn
    (~(tap in (~(get ju pus.hid) /out)))
  |=(a=bone [a %give git])
--
