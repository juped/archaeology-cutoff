/-  mess,user,users,zing,zong,zung
!:
=>  |%
    ++  axle
      $%  [%0 pad=@p air=(map path station)]
      ==
    ++  blitz
      $%  [%zong p=zong]
          [%user p=user]
      ==
    ++  idad  ,[p=@p q=@t]
    ++  iron
      $%  [%zongs p=(list zong)]
          [%users p=users]
          [%hymn p=manx]                                ::  HTML format
          [%json p=json]                                ::  JSON format
      ==
    ++  gift
      $%  [%mean ares]
          [%nice ~]
          [%rush blitz]
          [%rust iron]
      ==
    ++  hapt  ,[p=ship q=path]
    ++  move  ,[p=bone q=(mold note gift)]
    ++  note
              $?  $:  %g
              $%  [%mess p=hapt q=ship r=cage]
                  [%nuke p=hapt q=ship]
                  [%show p=hapt q=ship r=path]
              ==  ==  ==
    ++  sign
              $?  $:  %g
              $%  [%mean p=ares]
                  [%nice ~]
                  $:  %rush
                      $=  p
                      $%  [%user p=user]
                          [%zong p=zong]
                  ==  ==
                  $:  %rust
                      $=  p
                      $%  [%users p=users]
                          [%zongs p=(list zong)]
                  ==  ==
              ==  ==  ==
    ++  station
             $:  msg=(list zong)
                 sub=(unit bone)
                 ami=(set idad)
             ==
    --
|_  [hid=hide axle]
++  grab
  |=  sta=path
  (fall (~(get by air) sta) *station)
::
++  peer
  |=  [ost=bone you=ship pax=path]
  ^-  [(list move) _+>]
  :_  +>.$
  ?:  ?=(~ pax)
    [ost %give %rust %hymn page]~
  =+  ya=(grab t.pax)
  ?+  i.pax  !!
    %amigos
      [ost %give %rust %users (~(tap in ami.ya))]~
    %mensajes
      [ost %give %rust %zongs msg.ya]~
  ==
++  page
  ^-  manx
  ;html
    ;head
      ;title: Radio
      ;script(type "text/javascript", src "//use.typekit.net/fkv0sjk.js");
      ;script(type "text/javascript", src "//cdnjs.cloudflare.com/ajax/libs/jquery/2.1.1/jquery.min.js");
      ;script:'''
              try{Typekit.load();}catch(e){}
              '''
      ;link(rel "stylesheet", type "text/css", href "/gen/main/pub/src/chat/main.css");
    ==
    ;body
      ;div#c;
      ;script(type "text/javascript", src "/gen/main/lib/urb.js");
      ;script(type "text/javascript", src "/gen/main/pub/src/chat/main.js");
    ==
  ==
::
++  poke-zung
  |=  [ost=bone you=ship zug=zung]
  ^-  [(list move) _+>]
  ?.  =(you our.hid)
    [[ost %give %mean ~ %no-sos-mi-amigo ~]~ +>.$]
  ?-    -.zug
      %mess 
    ~&  [%send-mess zug]
    :_  +>.$  :_  ~ 
    :*  ost  %pass  /mesg  %g
        %mess  [pad %radio ~]  you  %zing  !>(zug)
    ==
  ::
      %init
    =+  ya=(grab q.zug)
    ?:  =(sub.ya ~)
      =.  pad  p.zug
      =.  sub.ya  `ost
      =.  air  (~(put by air) q.zug ya)
      :_  +>.$
      :~
        :*  ost  %pass  radi-m/q.zug  %g
            %show  [[p.zug /radio] you mensajes/q.zug]
        ==
        :*  ost  %pass  radi-a/q.zug  %g
            %show  [[p.zug /radio] you amigos/q.zug]
        ==
      ==
    [[ost %give %nice ~]~ +>.$]
  ==
::
++  pull
  |=  ost=bone
  ^-  [(list move) _+>]
  ?:  ?=([* ~ ~] sup.hid)
    :_  +>.$(pad *@p, air ~)
    %-  ^zing  %+  turn  (~(tap by air))
    |=  [pax=path sta=station]
    ?~  sub.sta
      ~
    :~  [u.sub.sta %pass radi-a/pax %g %nuke [[pad /radio] our.hid]]
        [u.sub.sta %pass radi-m/pax %g %nuke [[pad /radio] our.hid]]
    ==
  [~ +>.$]
::
++  pour
  |=  [ost=bone pax=path sih=sign]
  ^-  [(list move) _+>]
  ?~  pax  ~&  %chat-pour-strange-path  !!
  ~&  [%pour-mess pax]
  ?+    i.pax  ~&  %chat-pour-strange-path  !!
      %mesg
    ?>  ?=(?(%mean %nice) +<.sih)
    [[ost %give +.sih]~ +>.$]
  ::
    ?(%radi-a %radi-m)
    ?:  ?=(?(%nice %mean) +<.sih)
      :_  +>.$  
      ?:  ?=(%radi-a i.pax)  ~
      [ost %give +.sih]~
    =+  ya=(grab t.pax)
    =.  ya
      ?-    i.pax
          %radi-a
        %_    ya
            ami
          ^-  (set idad)
          ?-  +<.sih
            %rust  ?>(?=(%users -.p.sih) (sa p.p.sih))
            %rush
              ?>  ?=(%user -.p.sih)
              ?-  -.p.p.sih
                %in   (~(put in ami.ya) p.p.p.sih)
                %out  (~(del in ami.ya) p.p.p.sih)
              ==
          ==
        ==
          %radi-m
        %_   ya
            msg
          ^-  (list zong)
          ?-  +<.sih
            %rush  ?>(?=(%zong -.p.sih) [p.p.sih msg.ya])
            %rust  ?>(?=(%zongs -.p.sih) p.p.sih)
          ==
        ==
      ==
    =+  ^=  pout  ?:(=(i.pax %radi-a) %amigos %mensajes)
    :_  +>.$(air (~(put by air) t.pax ya))
    (send [pout t.pax] %give +.sih)
  ==
::
++  send
  |=  [pax=path msg=(mold note gift)]
  ^-  (list move)
  ~&  [%send pus.hid]
  %+  turn  (~(tap in (~(get ju pus.hid) pax)))
  |=(ost=bone [ost msg])
--
