::                                                      ::  ::
::::  /hook/door/octo-game/mar                          ::  ::  dependencies
  ::                                                    ::  ::
/?    310                                               ::  arvo
/-    *octo                                             ::  structures
/+    octo                                              ::  libraries
!:                                                      ::  ::
::::                                                    ::  ::  protocol
  ::                                                    ::  ::
|_  game                                                ::  game 
++  grab                                                ::  convert from
  |%
  ++  noun  game                                        ::  from %noun
  --
++  grow                                                ::  convert to %json
  |%
  ++  json  ^-  ^json                                   ::  to %json  
    =+  she=|=(ship s/(scot %p +<))
    %-  jobe
    :~  who/s/?:(who %x %o) 
        plx/?~(p.sag ~ (she u.p.sag))
        plo/?~(q.sag ~ (she u.q.sag))
        aud/a/(turn (~(tap by aud)) she)
        box/~(jon bo box)
        boo/~(jon bo boo)
    ==
  --
--
