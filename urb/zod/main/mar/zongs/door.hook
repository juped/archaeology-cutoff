::
::::  /hoon/core/zongs/mar
  ::
/?  314
/-  zong
!:
|_  zos=(list zong)
::
++  grab                                                ::  convert from
  |%
  ++  noun  (list zong)                                 ::  clam from %noun
  --
::
++  grow                                                ::  convert to
  |%
  ++  hymn                                              ::  convert to %hymn
    ^-  manx
    ;html
      ;head
        ;title: Zongs!
      ==
      ;body   ;table
        ;*  %+  turn  (flop zos)
        |=  zog=zong
        =.  p.zog  (lsh 6 1 (rsh 6 1 p.zog))            ::  round off subsecond
        ;tr   ;td: {<p.zog>}
              ;td: {<q.zog>}
              ;+  ?-  -.r.zog
                      %do
                    ;td: {(trip p.r.zog)}
                      %say
                    ;td: & {(trip p.r.zog)}
                      %exp
                    ;td
                      ;code:"{(trip p.r.zog)}"
                      ; \a0 {~(ram re q.r.zog)}
        ==          ==
      ==          ==
    ==          ==
  ++  json
    %-  jobe
    :~
      :-  %mensajes
      :-  %a
      %+  turn
        (flop zos)
      |=
        zog=zong
      =.  p.zog 
        (lsh 6 1 (rsh 6 1 p.zog))                       ::  round off subsecond
      %-  jobe  
      :~
        [%time (jape <p.zog>)]
        [%auth (jape <q.zog>)]
        :-  %mess  
        %+  joba  -.r.zog
        ?-  -.r.zog
            %do
          [%s p.r.zog]
            %say
          [%s p.r.zog]
            %exp
          [%a [%s p.r.zog] (turn (wash 0^80 q.r.zog) jape)]
        ==
      ==
    ==
  ++  html
    (crip (poxo hymn))
  ++  mime
    [/text/json (tact (pojo json))]
  --
--
