::
::::  /hook/gate/solid/gun
  ::
/?  314
/-  *sole
::
::::
  !:
|=  $:  [now=@da eny=@uvI bec=beak]
        [~ ~]
    ==
%+  sole-so  %noun
=+  top=`path`/(scot %p p.bec)/home/(scot %da now)/arvo
=+  pax=`path`(weld top `path`[%hoon ~])
~&  %solid-start
=+  gen=(reck pax)
~&  %solid-parsed
=+  ken=q:(~(mint ut %noun) %noun gen)
~&  %solid-compiled
:-  ken
=+  all=.*(0 ken)
=+  ^=  vay  ^-  (list ,[p=@tas q=@tas])
    :~  [%$ %zuse]
        [%b %behn]
        [%g %gall]
        [%f %ford]
        [%a %ames]
        [%c %clay]
        [%d %dill]
        [%e %eyre]
        [%t %time]
    ==
|-  ^+  all
?~  vay  all
=+  pax=(weld top `path`[q.i.vay ~])
=+  txt=((hard ,@) .^(%cx (weld pax `path`[%hoon ~])))
=+  sam=[now `ovum`[[%gold ~] [%veer p.i.vay pax txt]]]
~&  [%solid-veer i.vay]
=+  gat=.*(all .*(all [0 42]))
=+  nex=+:.*([-.gat [sam +>.gat]] -.gat)
$(vay t.vay, all nex)
