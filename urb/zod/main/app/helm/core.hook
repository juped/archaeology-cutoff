::                                                      ::  ::
::::  /hook/core/helm/app                               ::  ::
  ::                                                    ::  ::
/?  314                                                 ::  zuse version
/-  *console                                            ::  structures
/+  console                                             ::  libraries
::                                                      ::  ::
::::                                                    ::  ::
  !:                                                    ::  ::
=>  |%                                                  ::  principal structures
    ++  helm-house                                      ::  all state
      $:  %0                                            ::  state version
          
          hoc=(map bone helm-session)                   ::  consoles
      ==                                                ::
    ++  helm-session                                    ::
      $:  say=console-share                             ::
          mud=(unit (console-dialog ,@ud))              ::
      ==                                                ::
    ++  funk  (pair ,@ ,@)                              ::
    ++  helm-wish                                       ::
      $|  $?  %reset                                    ::  reset kernel
              %verb                                     ::  verbose mode
          ==                                            ::
      $%  [%reload p=(list term)]                       ::  reload vanes
          [%begin p=(qual ,@p ,@p ,@t gens)]            ::  start new ship
      ==                                                ::
    ++  dill-flog                                       ::  sent to %dill
      $%  [%veer p=@ta q=path r=@t]                     ::  install vane
          [%vega p=path]                                ::  reboot by path
          [%verb ~]                                     ::  verbose mode
      ==                                                ::
    ::                                                  ::
    ++  gift                                            ::  out result <-$
      $%  [%mean p=ares]                                ::  error
          [%nice ~]                                     ::  acknowledge
          [%rush %console-effect console-effect]        ::  effect 
      ==                                                ::
    ++  hapt  ,[p=ship q=path]                          ::  
    ++  move  ,[p=bone q=(mold note gift)]              ::
    ++  note-gall                                       ::  note to %gall
      $%  [%mess p=[p=ship q=path] q=ship r=cage]       ::
          [%show p=[p=ship q=path] q=ship r=path]       ::
          [%took p=[p=ship q=path] q=ship]              ::
      ==                                                ::
    ++  note-dill                                       ::  system command
      $%  [%flog p=dill-flog]                           ::
      ==                                                ::
    ++  note                                            ::  out request $->
      $%  [%d note-dill]                                ::
          [%g note-gall]                                ::
      ==                                                ::
    --                                                  ::
::                                                      ::
::::                                                    ::
  ::                                                    ::
|_  $:  hid=hide                                        ::  system state
        helm-house                                      ::  program state
    ==                                                  ::
++  he                                                  ::  per session
  |_  [[ost=bone moz=(list move)] helm-session]         ::
  ++  he-abet                                           ::  resolve
    [(flop moz) %_(+> hoc (~(put by hoc) ost +<+))]     ::
  ::                                                    ::
  ++  he-give                                           ::  emit gift
    |=  git=gift
    %_(+> moz [[ost %give git] moz])
  ::
  ++  he-wish-reset
    ^+  .
    =-  %_(+ moz (weld zum moz))
    ^=  zum  ^-  (list move)
    =+  top=`path`/(scot %p our.hid)/arvo/(scot %da lat.hid)
    :-  [ost %pass /reset %d %flog %vega (weld top `path`/hoon)]
    %+  turn
      ^-  (list ,[p=@tas q=@tas])
      :~  [%$ %zuse]
          [%a %ames]
          [%c %clay]
          [%d %dill]
          [%e %eyre]
          [%f %ford]
          [%g %gall]
          [%t %time]
      ==
    |=  [p=@tas q=@tas]
    =+  pax=`path`(welp top /[q])
    =+  txt=((hard ,@) .^(%cx (welp pax /hoon)))
    [ost %pass /reset %d %flog %veer p pax txt]
  ::
  ++  he-wish-reload 
    |=  all=(list term)
    %_    +>.$
        moz
      %-  weld
      :_  moz
      %+  turn  all
      =+  ark=(arch .^(%cy /(scot %p our.hid)/arvo/(scot %da lat.hid)))
      =+  van=(~(tap by r.ark))
      |=  nam=@tas
      =.  nam
        ?.  =(1 (met 3 nam))
          nam
        =+  ^-  zaz=(list ,[p=span ~])
            (skim van |=([a=term ~] =(nam (end 3 1 a))))
        ?>  ?=([[@ ~] ~] zaz)
        `term`p.i.zaz
      =+  tip=(end 3 1 nam)
      =+  pax=[(scot %p our.hid) %arvo (scot %da lat.hid) nam %hoon ~]
      :*  ost
          %pass
          /reload
          %d
          %flog
          [%veer ?:(=('z' tip) %$ tip) pax (,@ .^(%cx pax))]
      ==
    ==
  ::
  ++  he-wish-begin
    |=  mes=(qual ,@p ,@p ,@t gens)
    %_    +>
        moz  
      :_  moz
      [ost %pass /begin %g %mess [our.hid /began] our.hid %began-args !>(mes)]
    ==
  ::
  ++  he-wish-verb
    %_    .
        moz
      :_  moz
      [ost %pass /verb %d %flog %verb ~]
    ==
  ::
  ++  he-wish
    |=  wus=(unit helm-wish)
    ^+  +>
    ?~  wus  +>
    ?-  u.wus 
      %verb        he-wish-verb
      %reset       he-wish-reset
      [%reload *]  !!  ::  (he-wish-reload +.u.wus)  
      [%begin *]   (he-wish-begin +.u.wus)
    ==
  --
::
++  hake                                                ::  poke core
  |=  [ost=bone her=ship]
  ?>  =(her our.hid)
  ~(. he [ost [ost %give %nice ~]~] (fall (~(get by hoc) ost) *helm-session))
::
++  poke-helm-reset
  |=  [ost=bone her=ship ~]
  ~&  %poke-helm-reset
  he-abet:he-wish-reset:(hake ost her)
::
++  poke-helm-reload
  |=  [ost=bone her=ship all=(list term)]
  ~&  %poke-helm-reload
  he-abet:(he-wish-reload:(hake ost her) all)
--
